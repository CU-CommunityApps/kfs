!
! Copyright 2006 The Kuali Foundation.
! 
! Licensed under the Educational Community License, Version 1.0 (the "License");
! you may not use this file except in compliance with the License.
! You may obtain a copy of the License at
! 
! http://www.opensource.org/licenses/ecl1.php
! 
! Unless required by applicable law or agreed to in writing, software
! distributed under the License is distributed on an "AS IS" BASIS,
! WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
! See the License for the specific language governing permissions and
! limitations under the License.
! DO NOT add comments before the blank line below, or they will disappear.

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $Id: 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

#include "kul_error.h"
#include "kul_check.h"
#include "kul_common.h"
#include "kul_enter.h"


begin-heading 14

   #debug display 'entering HEADING'

   if #change_page_number = 1
      #debug display '- change_page_number is set'
      let #page_number = 1
      let #change_page_number = 0
   else
      let #page_number = #page_number + 1
   end-if

   #debug display 'current page number is ' NOLINE
   #debug display #page_number

   if #print_lf = 1
      #debug display '- printing linefeed'
      let $lb = chr(12) 
      print $lb (1,1)
      print ' .' (,2) 
   end-if

   ! add a couple spaces to the first line on each page.
   print '  ' (,1)
   print $report_date (+2,2)
   print 'PAGE' (,121)
   print #page_number (,125,8) edit 99999999


   print 'RC TITLE:' (+1,81)
   print $old_rc_shrt_nm (,105)

   print 'FOR THE MONTH ENDING ' (+1,2)
   print $display_for_month (,23)
   print 'RC MANAGER:' (,81)
   print $old_person_nm (,105,27)

   print 'PERIOD:' (+1,2)
   print $display_univ_fscpd_cd (,10) edit 99
   print 'RC CODE:' (,81)
   print $old_rc_cd (,105)

   print 'CONSOLIDATED STATUS' (+1,2)
   print 'CHART:' (,81)
   print $old_fin_coa_cd (,105,27)

   print 'REPORT ID:  CONS_STAT' (+1,2)
   print 'FUND GROUP:' (,81)
   print $old_fund_grp_nm (,105, 27)

   print 'SUB-FUND GROUP:' (+1,81)
   print $old_sub_fund_grp_desc (,105,27)


   if $old_fund_grp_cd = 'CG'
      print 'CUMULATIVE' (+2,30)
   else
      print ' ' (+2,53)
   end-if

   print 'CURRENT' (+1,31)
   if $old_fund_grp_cd = 'CG'
      print 'CUMULATIVE' (,50)
   else
      print 'YTD' (,53)
   end-if
   print 'OUTSTANDING' (,73)
   print 'BUDGET' (,97)
   print 'PERCENT OF' (,116)

   print 'BUDGET' (+1,31)
   print 'ACTUAL' (,53)
   print 'ENCUMBRANCES' (,72)
   print 'BALANCE' (,97)
   print 'BUDGET COMMITTED' (,116)
 
   print '--------------------' (+1,24)
   print '--------------------' (,46)
   print '--------------------' (,68)
   print '--------------------' (,90)
   print '----------------' (,116)

   let #line_count = 0
   let #print_lf = 0

end-heading




begin-footing 11

   if #print_footer = 1 
 
      print 'INFORMATION ABOUT THIS REPORT' (+2,2)
      print 'THE CONSOLIDATED ACCOUNT STATUS REPORT SHOWS A YEAR-TO-DATE SUMMARY OF BUDGET, ACTUAL, AND ENCUMBRANCE TRANSACTIONS' (+1,2)
      print 'AT THE CONSOLIDATION LEVEL, EXCEPT FOR COMPENSATION, WHICH IS SHOWN AT THE LEVEL LEVEL.' (+1,2)
      print 'THE CONTRACT AND GRANT ACCOUNTS SHOW A CUMULATIVE SUMMARY FROM THE INCEPTION OF THE ACCOUNT.' (+1,2)
      print 'THE ENCUMBRANCES INCLUDE INTERNAL, EXTERNAL, AND PRE-ENCUMBRANCES.' (+1,2)
      print 'THE FORMULA FOR THE BUDGET BALANCE COLUMN IS:' (+1,2)

      if $old_fund_grp_cd = 'CG'
         print '(CUMULATIVE CURRENT BUDGET - (CUMULATIVE ENDING ACTUAL + OUTSTANDING ENCUMBRANCES))' (+1,2)
      else
         print '(CURRENT BUDGET - (YTD ENDING ACTUAL + OUTSTANDING ENCUMBRANCES))' (+1,2)
      end-if

      print 'THE FORMULA FOR THE % OF BUDGET COMMITTED COLUMN IS:' (+1,2)

      if $old_fund_grp_cd = 'CG'
         print '(CUMULATIVE ENDING ACTUAL + OUTSTANDING ENCUMBRANCES) / CUMULATIVE CURRENT BUDGET' (+1,2)
      else
         print '((YTD ENDING ACTUAL + OUTSTANDING ENCUMBRANCES) / CURRENT BUDGET)' (+1,2)
      end-if


      let #print_footer = 0

   end-if

end-footing




begin-program

   Do GetParms
   Do Main

end-program



begin-procedure GetParms

   Do SetConstantGlobal

   input $fiscal_year 'Enter Fiscal Year'
   input $fiscal_period 'Enter Fiscal Period' 

   let $display_univ_fscpd_cd = $fiscal_period

   let #calendar_yr = to_number( $fiscal_year )
   let #fiscal_period = to_number( $fiscal_period )
   if ( #fiscal_period <= 6 )
      let #calendar_yr = #calendar_yr - 1
   end-if
   let $calendar_yr = to_char( #calendar_yr )

   ! determine the number of days in February for the current year
   Do SetFebruaryEnd( #calendar_yr, $feb_day_last )

   evaluate $display_univ_fscpd_cd
      when = '01'
         let $display_for_month = 'JULY 31, ' || $calendar_yr
         break
      when = '02'
         let $display_for_month = 'AUG. 31, ' || $calendar_yr
         break
      when = '03'
         let $display_for_month = 'SEP. 30, ' || $calendar_yr
         break
      when = '04'
         let $display_for_month = 'OCT. 31, ' || $calendar_yr
         break
      when = '05'
         let $display_for_month = 'NOV. 30, ' || $calendar_yr
         break
      when = '06'
         let $display_for_month = 'DEC. 31, ' || $calendar_yr
         break
      when = '07'
         let $display_for_month = 'JAN. 31, ' || $calendar_yr
         break
      when = '08'
         let $display_for_month = 'FEB. ' || $feb_day_last || ', ' || $calendar_yr
         break
      when = '09'
         let $display_for_month = 'MAR. 31, ' || $calendar_yr
         break
      when = '10'
         let $display_for_month = 'APR. 30, ' || $calendar_yr
         break
      when = '11'
         let $display_for_month = 'MAY 31, ' || $calendar_yr
         break
      when = '12'
         let $display_for_month = 'JUNE 30, ' || $calendar_yr
         break
      when = '13'
         let $display_for_month = 'JUNE 30, ' || $calendar_yr
         break
      when-other
         let $display_for_month = ' '
   end-evaluate

end-procedure







begin-procedure Main

   ! Set any variables
   let #page_number = 0
   let #print_bar = 0
   let #total_cash_bal = 0
   let #total_bdgt_ytd = 0
   let #total_actl_ytd = 0
   let #total_ot_encb = 0
   let #total_bdgt_bal = 0

BEGIN-SELECT
TO_CHAR(SYSDATE, 'MM/DD/YY')                 &report_date
a.fin_coa_cd                                 &fin_coa_cd
a.rc_shrt_nm                                 &rc_shrt_nm          ()        ON-BREAK LEVEL=1 PRINT=NEVER PROCEDURE=MakeNewPageRenum
a.rc_shrt_nm as r2                                                ()        ON-BREAK LEVEL=1 PRINT=NEVER AFTER=PrintFooter
a.rc_cd                                      &rc_cd
a.fund_grp_cd                                &fund_grp_cd
a.fund_grp_nm                                &fund_grp_nm
a.sub_fndgrp_rsrt_cd                                              ()        ON-BREAK LEVEL=2 PRINT=NEVER AFTER=MakeNewPage
a.sub_fund_grp_desc                          &sub_fund_grp_desc   ()        ON-BREAK LEVEL=3 PRINT=NEVER AFTER=PrintCashBalance
b.person_nm                                  &person_nm
a.object_name                                &object_name         ()        ON-BREAK LEVEL=4 PRINT=NEVER AFTER=PrintObjectTotal
a.cash_bal                                   &cash_bal
a.name                                       &name                (+1,10)
a.bdgt_ytd                                   &bdgt_ytd            (,24,20)  edit 999,999,999,999.99pf
a.actl_ytd                                   &actl_ytd            (,46,20)  edit 999,999,999,999.99pf
a.ot_encb                                    &ot_encb             (,68,20)  edit 999,999,999,999.99pf
a.bdgt_bal                                   &bdgt_bal            (,90,20)  edit 999,999,999,999.99pf
a.pct_bdgt                                                        (,116,16) edit 999,999,999.99pf


   let $report_date = &report_date
   let $old_fin_coa_cd = &fin_coa_cd
   let $old_rc_shrt_nm = &rc_shrt_nm
   let $old_rc_cd = &rc_cd
   let $old_fund_grp_cd = &fund_grp_cd
   let $old_fund_grp_nm = &fund_grp_nm
   let $old_sub_fund_grp_desc = &sub_fund_grp_desc
   let $old_person_nm = &person_nm
   let $old_object_name = &object_name
   let #cash_bal = &cash_bal
   let #bdgt_ytd = &bdgt_ytd
   let #actl_ytd = &actl_ytd
   let #ot_encb = &ot_encb
   let #bdgt_bal = &bdgt_bal

   let #total_cash_bal = #total_cash_bal + #cash_bal
   let #total_bdgt_ytd = #total_bdgt_ytd + #bdgt_ytd
   let #total_actl_ytd = #total_actl_ytd + #actl_ytd
   let #total_ot_encb = #total_ot_encb + #ot_encb
   let #total_bdgt_bal = #total_bdgt_bal + #bdgt_bal
 
   let #print_bar = 1


FROM     kul_cons_stat_v a,
	 fs_universal_usr_t b

WHERE    a.org_mgr_unvl_id = b.person_unvl_id (+)


ORDER BY a.fin_coa_cd,
         a.rc_shrt_nm,
         a.fund_grp_rsrt_cd,
         a.sub_fndgrp_rsrt_cd,
         a.sub_fund_grp_desc,
         a.object_sort,
         a.object_name,
         a.fin_objcon_rsrt_cd,
         a.aggreg_code,
         a.name

END-SELECT


end-procedure




begin-procedure MakeNewPageRenum

   new-page
   let #change_page_number = 1
   let #print_bar = 0

end-procedure


begin-procedure PrintFooter

   let #print_footer = 1

end-procedure


begin-procedure MakeNewPage

   new-page

end-procedure


begin-procedure PrintCashBalance

   let #printed_cash_bal = 0

   let $total_cash_bal = #total_cash_bal

   evaluate $old_fund_grp_cd
      when = 'AE'
         print 'CASH BALANCE' (+1,6)
         print $total_cash_bal (,46,20)  edit 999,999,999,999.99pf
         print '**********************************************************************************************************************************' (+2,2)
         let #printed_cash_bal = 1
         break
      when = 'AF'
         print 'CASH BALANCE' (+1,6)
         print $total_cash_bal (,46,20)  edit 999,999,999,999.99pf
         print '**********************************************************************************************************************************' (+2,2)
         let #printed_cash_bal = 1
         break
      when = 'CG'
         print 'CASH BALANCE' (+1,6)
         print $total_cash_bal (,46,20)  edit 999,999,999,999.99pf
         print '**********************************************************************************************************************************' (+2,2)
         let #printed_cash_bal = 1
         break
      when = 'CL'
         print 'CASH BALANCE' (+1,6)
         print $total_cash_bal (,46,20)  edit 999,999,999,999.99pf
         print '**********************************************************************************************************************************' (+2,2)
         let #printed_cash_bal = 1
         break
      when = 'DS'
         print 'CASH BALANCE' (+1,6)
         print $total_cash_bal (,46,20)  edit 999,999,999,999.99pf
         print '**********************************************************************************************************************************' (+2,2)
         let #printed_cash_bal = 1
         break
      when = 'EN'
         print 'CASH BALANCE' (+1,6)
         print $total_cash_bal (,46,20)  edit 999,999,999,999.99pf
         print '**********************************************************************************************************************************' (+2,2)
         let #printed_cash_bal = 1
         break
      when = 'LF'
         print 'CASH BALANCE' (+1,6)
         print $total_cash_bal (,46,20)  edit 999,999,999,999.99pf
         print '**********************************************************************************************************************************' (+2,2)
         let #printed_cash_bal = 1
         break
      when = 'OF'
         print 'CASH BALANCE' (+1,6)
         print $total_cash_bal (,46,20)  edit 999,999,999,999.99pf
         print '**********************************************************************************************************************************' (+2,2)
         let #printed_cash_bal = 1
         break
      when = 'PF'
         print 'CASH BALANCE' (+1,6)
         print $total_cash_bal (,46,20)  edit 999,999,999,999.99pf
         print '**********************************************************************************************************************************' (+2,2)
         let #printed_cash_bal = 1
         break
      when = 'RF'
         print 'CASH BALANCE' (+1,6)
         print $total_cash_bal (,46,20)  edit 999,999,999,999.99pf
         print '**********************************************************************************************************************************' (+2,2)
         let #printed_cash_bal = 1
         break
   end-evaluate

   let #total_cash_bal = 0


end-procedure



begin-procedure PrintObjectTotal

   let $total_bdgt_ytd = #total_bdgt_ytd
   let $total_actl_ytd = #total_actl_ytd
   let $total_ot_encb = #total_ot_encb
   let $total_bdgt_bal = #total_bdgt_bal

   if #total_bdgt_ytd <> 0
      let $total_pct_bdgt = ((#total_actl_ytd + #total_ot_encb) / #total_bdgt_ytd) * 100
   else
      let $total_pct_bdgt = 0
   end-if



   if $old_object_name = 'EXPENSES'

      let $line = '** EXPENDITURES NET TRANSFERS IN'
      print $line (+2,2)
      print $total_bdgt_ytd (,24,20)  edit 999,999,999,999.99pf
      print $total_actl_ytd (,46,20)  edit 999,999,999,999.99pf
      print $total_ot_encb  (,68,20)  edit 999,999,999,999.99pf
      print $total_bdgt_bal (,90,20)  edit 999,999,999,999.99pf
      print $total_pct_bdgt (,116,16) edit 999,999,999.99pf
      print '' (+1,2)

   else

      let $line = '** TOTAL ' || $old_object_name

      print $line (+2,2)
      print $total_bdgt_ytd (,24,20)  edit 999,999,999,999.99pf
      print $total_actl_ytd (,46,20)  edit 999,999,999,999.99pf
      print $total_ot_encb  (,68,20)  edit 999,999,999,999.99pf
      print $total_bdgt_bal (,90,20)  edit 999,999,999,999.99pf
      print $total_pct_bdgt (,116,16) edit 999,999,999.99pf
      print '' (+1,2)
   end-if

   let #total_bdgt_ytd = 0
   let #total_actl_ytd = 0
   let #total_ot_encb = 0
   let #total_bdgt_bal = 0

end-procedure

