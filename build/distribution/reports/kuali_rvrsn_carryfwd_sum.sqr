!$id:
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!       Purpose: Reversion and Carry Forward Summary (Year End Report)
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


begin-heading 12

   if #change_page_number = 1
      #debug display '- change_page_number is set'
      let #page_number = 1
      let #change_page_number = 0
   else
      let #page_number = #page_number + 1
   end-if

   #debug display 'current page number is ' NOLINE
   #debug display #page_number

   ! add a couple spaces to the first line on each page.
   print '  ' (,1)

   print $report_date (+1,2)
   print 'REVERSION AND CARRYFORWARD SUMMARY BY SUB ACCOUNT' (,35)
   print 'PAGE' (,114)
   print #page_number (,119,5) edit 99999
   


   let $line = 'RUN DATE OF ' || $report_date
   print $line (+1,50)
   
   print $for_fin_coa_cd_line (+2,50) 
   print $for_rc_cd_line (+1,50) 
   
   print 'BUDGET' (+2,77)
   print 'BAL AVAIL' (,95)
   print 'OUTSTANDING' (,113)

   print 'BUDGET' (+1,38)
   print 'ACTUAL' (,58)
   print 'BALANCE' (,77)
   print 'TO REVERT' (,95)
   print 'ENCUMBRANCE' (,113)

   print '------' (+1,38)
   print '------' (,58)
   print '-------' (,77)
   print '---------' (,95)
   print '-----------' (,113)

   if $report_type = 'account'
      print 'CHART:' (+2,2)
      print $old_fin_coa_cd (,12)
      let $header_line = 'RESP CTR: ' || $old_rc_cd || ' ' || $old_rc_nm  
      print $header_line (+1,2)      
   end-if

   if $report_type = 'rc'
      print $old_rc_nm (+2,2)
      print 'CHART:' (+1,3)   
      print $old_fin_coa_cd (,12)
   end-if

   if $report_type = 'campus'
      print 'CHART:' (+2,2)   
      print $old_fin_coa_cd (,12)
      print ' ' (+1,2)
   end-if
  
   let #line_count = 0
   let #print_lf = 0

end-heading



begin-program

   Do GetParms
   Do Main

end-program



begin-procedure GetParms

   show 'Input Chart Code(s) 1 at a time - Optional, hit enter when done'
   let $fin_coa_cd = ''
   let $select_fin_coa_cd = '' 
   input $fin_coa_cd
   let $fin_coa_cd = upper($fin_coa_cd)
   if rtrim($fin_coa_cd,' ') > ''
      let $select_fin_coa_cd = '  AND (a.fin_coa_cd = ''' || $fin_coa_cd || '''' 
      let $for_fin_coa_cd_line = 'FOR CHART CODE(S):  ' || $fin_coa_cd
   end-if
   while ($fin_coa_cd <> '') ! the end of input from the parm file 
      input $fin_coa_cd
      let $fin_coa_cd = upper($fin_coa_cd)
    
      if rtrim($fin_coa_cd,' ') > ''
         let $select_fin_coa_cd = $select_fin_coa_cd || ' OR a.fin_coa_cd = ''' || $fin_coa_cd || '''' 
         let $for_fin_coa_cd_line = $for_fin_coa_cd_line || ',' || $fin_coa_cd
      else
         let $select_fin_coa_cd = $select_fin_coa_cd || ')'
      end-if
   end-while 

   if $select_fin_coa_cd = ''
      let $for_fin_coa_cd_line = 'FOR CHART CODE(S):  ALL'
   end-if
   
   show 'Select Chart Codes: ' $select_fin_coa_cd


   show 'Input RC Code(s) 1 at a time - Optional, hit enter when done'
   let $rc_cd = ''
   let $select_rc_cd = '' 
   input $rc_cd
   let $rc_cd = upper($rc_cd)
   if rtrim($rc_cd,' ') > ''
      let $select_rc_cd = '  AND (b.rc_cd like ''' || $rc_cd || '''' 
      let $for_rc_cd_line = 'FOR RC CODE(S):     ' || $rc_cd
   end-if
   while ($rc_cd <> '') ! the end of input from the parm file 
      input $rc_cd
      let $rc_cd = upper($rc_cd)
      if rtrim($rc_cd,' ') > ''
         let $select_rc_cd = $select_rc_cd || ' OR b.rc_cd like ''' || $rc_cd || '''' 
         let $for_rc_cd_line = $for_rc_cd_line || ',' || $rc_cd
      else
         let $select_rc_cd = $select_rc_cd || ')'
      end-if
   end-while 

   if $select_rc_cd = ''
      let $for_rc_cd_line = 'FOR RC CODE(S):     ALL'
   end-if
   show 'Select RC Codes: ' $select_rc_cd

end-procedure




begin-procedure Main

   let #page_number  = 0
   let #recordsAcct  = 0
   let #recordsRC    = 0
   let #recordsCampus = 0
   
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!  Print out report by account

let $report_type = 'account'

BEGIN-SELECT
org.rc_cd                                   &rc_cd         ()      ON-BREAK LEVEL=1 PRINT=NEVER PROCEDURE=MakeNewPageRenum
TO_CHAR(SYSDATE, 'MM/DD/YYYY')              &report_date
rc.rc_nm                                    &rc_nm         
a.fin_coa_cd                                &fin_coa_cd   
a.account_nbr                               &account_nbr
a.sub_acct_nbr                              &sub_acct_nbr
account.account_nm                          &account_nm
subaccount.sub_acct_nm                      &sub_acct_nm
a.account_nbr || a.sub_acct_nbr as a1                      ()      ON-BREAK LEVEL=2 PRINT=NEVER BEFORE=PrintAccountInfo
a.account_nbr || a.sub_acct_nbr as a2                      ()      ON-BREAK LEVEL=2 PRINT=NEVER AFTER=PrintAccountTotals
b.org_rvrsn_ctgry_nm                                       (+1,11)
a.org_tot_bdgt_amt as budget                               (,27)   edit 9999,999,999,999.99
a.org_tot_actl_amt as actual                               (,47)   edit 9999,999,999,999.99
a.org_tot_bdgt_amt - a.org_tot_actl_amt                    (,67)   edit 9999,999,999,999.99
a.org_tot_avail_amt                                        (,87)   edit 9999,999,999,999.99
a.org_tot_encum_amt                                        (,107)  edit 9999,999,999,999.99

   let $report_date = &report_date
   let $old_rc_cd = &rc_cd
   let $old_rc_nm = &rc_nm
   let $old_account_nbr = &account_nbr
   let $old_sub_acct_nbr = &sub_acct_nbr
   let $old_fin_coa_cd = &fin_coa_cd


FROM gl_org_rvrsn_ctgry_amt_t a,
     ca_org_rvrsn_ctgry_t b,
     ca_account_t account,
     ca_org_t org,
	 ca_rc_t rc,
	 ca_sub_acct_t subaccount


WHERE a.org_rvrsn_ctgry_cd = b.org_rvrsn_ctgry_cd
AND   a.fin_coa_cd = account.fin_coa_cd
AND   a.account_nbr = account.account_nbr
AND   account.fin_coa_cd = org.fin_coa_cd
AND   account.org_cd = org.org_cd
AND   org.rc_cd = rc.rc_cd
AND   a.fin_coa_cd = subaccount.fin_coa_cd (+)
AND   a.account_nbr = subaccount.account_nbr (+)
AND   a.sub_acct_nbr = subaccount.sub_acct_nbr (+)
AND   (a.org_tot_bdgt_amt <> 0 OR a.org_tot_actl_amt <> 0 OR a.org_tot_avail_amt <> 0)

ORDER BY a.fin_coa_cd,
         org.rc_cd,
         a.account_nbr,
         a.sub_acct_nbr
END-SELECT





!!!!!!!!!!!!!!!!!!!!!!!!!
!  Print out report by RC


Do MakeNewPageRenum
let $report_type = 'rc' 

BEGIN-SELECT
org.rc_cd as r1                             &rc_cd_rc      ()      ON-BREAK LEVEL=1 PRINT=NEVER PROCEDURE=MakeNewPageRenum
a.fin_coa_cd                                &fin_coa_cd_rc
rc.rc_nm                                    &rc_nm_rc         
org.rc_cd as r2                                            ()      ON-BREAK LEVEL=2 PRINT=NEVER BEFORE=PrintRCInfo
org.rc_cd as r3                                            ()      ON-BREAK LEVEL=2 PRINT=NEVER AFTER=PrintRCTotals
b.org_rvrsn_ctgry_nm as rc_ctgry                           (+1,11)
SUM(a.org_tot_bdgt_amt)  as rc_sum1                        (,27)   edit 9999,999,999,999.99
SUM(a.org_tot_actl_amt)  as rc_sum2                        (,47)   edit 9999,999,999,999.99
SUM(a.org_tot_bdgt_amt - a.org_tot_actl_amt) as rc_sum3    (,67)   edit 9999,999,999,999.99
SUM(a.org_tot_avail_amt) as rc_sum4                        (,87)   edit 9999,999,999,999.99
SUM(a.org_tot_encum_amt) as rc_sum5                        (,107)  edit 9999,999,999,999.99

   let $old_fin_coa_cd = &fin_coa_cd_rc
   let $old_rc_cd = &rc_cd_rc
   let $old_rc_nm = &rc_nm_rc


FROM gl_org_rvrsn_ctgry_amt_t a,
     ca_org_rvrsn_ctgry_t b,
     ca_account_t account,
     ca_org_t org,
	 ca_rc_t rc

WHERE a.org_rvrsn_ctgry_cd = b.org_rvrsn_ctgry_cd
AND   a.fin_coa_cd = account.fin_coa_cd
AND   a.account_nbr = account.account_nbr
AND   account.fin_coa_cd = org.fin_coa_cd
AND   account.org_cd = org.org_cd
AND   org.rc_cd = rc.rc_cd
AND   (a.org_tot_bdgt_amt <> 0 OR a.org_tot_actl_amt <> 0 OR a.org_tot_avail_amt <> 0)


GROUP BY a.fin_coa_cd,
         org.rc_cd,
         rc.rc_nm,
         a.org_rvrsn_ctgry_cd,
         b.org_rvrsn_ctgry_nm

ORDER BY a.fin_coa_cd,
         org.rc_cd,
         rc.rc_nm,
         a.org_rvrsn_ctgry_cd
END-SELECT



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!  Print out report by campus

Do MakeNewPageRenum
let $report_type = 'campus' 

BEGIN-SELECT
a.fin_coa_cd as c1                     &fin_coa_cd_campus  ()      ON-BREAK LEVEL=1 PRINT=NEVER PROCEDURE=MakeNewPageRenum
a.fin_coa_cd as c2                                         ()      ON-BREAK LEVEL=2 PRINT=NEVER BEFORE=PrintCampusInfo
a.fin_coa_cd as c3                                         ()      ON-BREAK LEVEL=2 PRINT=NEVER AFTER=PrintCampusTotals
b.org_rvrsn_ctgry_nm as c_ctgry                            (+1,11)
SUM(a.org_tot_bdgt_amt)  as c_sum1                         (,27)   edit 9999,999,999,999.99
SUM(a.org_tot_actl_amt)  as c_sum2                         (,47)   edit 9999,999,999,999.99
SUM(a.org_tot_bdgt_amt - a.org_tot_actl_amt) as c_sum3     (,67)   edit 9999,999,999,999.99
SUM(a.org_tot_avail_amt) as c_sum4                         (,87)   edit 9999,999,999,999.99
SUM(a.org_tot_encum_amt) as c_sum5                         (,107)  edit 9999,999,999,999.99

   let $old_fin_coa_cd = &fin_coa_cd_campus

FROM gl_org_rvrsn_ctgry_amt_t a,
     ca_org_rvrsn_ctgry_t b

WHERE a.org_rvrsn_ctgry_cd = b.org_rvrsn_ctgry_cd
AND   (a.org_tot_bdgt_amt <> 0 OR a.org_tot_actl_amt <> 0 OR a.org_tot_avail_amt <> 0)

GROUP BY a.fin_coa_cd,
         a.org_rvrsn_ctgry_cd,
         b.org_rvrsn_ctgry_nm

ORDER BY a.fin_coa_cd,
         a.org_rvrsn_ctgry_cd
END-SELECT



end-procedure 



begin-procedure PrintAccountInfo

   let $account_nbr = &account_nbr
   let $sub_acct_nbr = &sub_acct_nbr
   let $account_nm = &account_nm
   let $sub_acct_nm = &sub_acct_nm

   if $sub_acct_nbr = '-----'
      let $account_line = $account_nbr || ' ' || $sub_acct_nbr || ' ' || $account_nm
   else 
      let $account_line = $account_nbr || ' ' || $sub_acct_nbr || ' ' || $sub_acct_nm
   end-if
   
   print $account_line (+2,2)
   print ' ' (+1,2)

end-procedure



begin-procedure PrintAccountTotals

   let $where_fin_coa_cd = 'a.fin_coa_cd = ''' || $old_fin_coa_cd || ''''
   let $where_account_nbr = 'a.account_nbr = ''' || $old_account_nbr || ''''
   let $where_sub_acct_nbr = 'a.sub_acct_nbr = ''' || $old_sub_acct_nbr || ''''

BEGIN-SELECT
'ACCT TOTAL'                                               (+2,15)
SUM(a.org_tot_bdgt_amt)                                    (,27)   edit 9999,999,999,999.99
SUM(a.org_tot_actl_amt)                                    (,47)   edit 9999,999,999,999.99
SUM(a.org_tot_bdgt_amt - a.org_tot_actl_amt)               (,67)   edit 9999,999,999,999.99
SUM(a.org_tot_avail_amt)                                   (,87)   edit 9999,999,999,999.99
SUM(a.org_tot_encum_amt)                                   (,107)  edit 9999,999,999,999.99


FROM gl_org_rvrsn_ctgry_amt_t a

WHERE (a.org_tot_bdgt_amt <> 0 OR a.org_tot_actl_amt <> 0 OR a.org_tot_avail_amt <> 0)
AND   [$where_fin_coa_cd]
AND   [$where_account_nbr]
AND   [$where_sub_acct_nbr]
END-SELECT


BEGIN-SELECT
'BUDGET BALANCE CARRIED FORWARD TO ACCT ' || $old_account_nbr    (+2,38)
a.org_tot_cf_amt                                                 (,87)  edit 9999,999,999,999.99
'BUDGET BALANCE REVERTED TO ACCT ' || org_rev.bdgt_rvrsnacct_nbr (+1,45)
a.org_tot_rvrsn_amt                                              (,87)  edit 9999,999,999,999.99

FROM gl_org_rvrsn_unit_wrk_t a,
     ca_account_t account,
     ca_org_reversion_t org_rev

WHERE a.fin_coa_cd = account.fin_coa_cd 
AND   a.account_nbr = account.account_nbr
AND   account.fin_coa_cd = org_rev.fin_coa_cd 
AND   account.org_cd = org_rev.org_cd 
AND   org_rev.univ_fiscal_yr = (SELECT c.sh_parm_txt FROM sh_parm_t c WHERE c.sh_parm_nm = 'ANNUAL_CLOSING_FISCAL_YEAR')
AND   [$where_fin_coa_cd]
AND   [$where_account_nbr]
AND   [$where_sub_acct_nbr]
END-SELECT

print ' ' (+1,2)

end-procedure



begin-procedure PrintRCInfo

   print &rc_cd_rc (+3,2)
   print &rc_nm_rc (,+1)

end-procedure



begin-procedure PrintRCTotals

   let $where_fin_coa_cd = 'a.fin_coa_cd = ''' || $old_fin_coa_cd || ''''
   let $where_rc_cd = 'org.rc_cd = ''' || $old_rc_cd || ''''

BEGIN-SELECT
'RC TOTAL'                                                 (+2,15)
SUM(a.org_tot_bdgt_amt)  as rc_total1                      (,27)   edit 9999,999,999,999.99
SUM(a.org_tot_actl_amt)  as rc_total2                      (,47)   edit 9999,999,999,999.99
SUM(a.org_tot_bdgt_amt - a.org_tot_actl_amt) as rc_total3  (,67)   edit 9999,999,999,999.99
SUM(a.org_tot_avail_amt) as rc_total4                      (,87)   edit 9999,999,999,999.99
SUM(a.org_tot_encum_amt) as rc_total5                      (,107)  edit 9999,999,999,999.99


FROM gl_org_rvrsn_ctgry_amt_t a,
     ca_org_rvrsn_ctgry_t b,
     ca_account_t account,
     ca_org_t org

WHERE a.org_rvrsn_ctgry_cd = b.org_rvrsn_ctgry_cd
AND   a.fin_coa_cd = account.fin_coa_cd
AND   a.account_nbr = account.account_nbr
AND   account.fin_coa_cd = org.fin_coa_cd
AND   account.org_cd = org.org_cd
AND   (a.org_tot_bdgt_amt <> 0 OR a.org_tot_actl_amt <> 0 OR a.org_tot_avail_amt <> 0)
AND   [$where_fin_coa_cd]
AND   [$where_rc_cd]
END-SELECT


BEGIN-SELECT
'RC BUDGET BALANCE CARRIED FORWARD'                              (+2,38)
SUM(a.org_tot_cf_amt)  as rc_total6                              (,87)  edit 9999,999,999,999.99
'RC BUDGET BALANCE REVERTED'                                     (+1,45)
SUM(a.org_tot_rvrsn_amt) as rc_total7                            (,87)  edit 9999,999,999,999.99

FROM gl_org_rvrsn_unit_wrk_t a,
     ca_account_t account,
     ca_org_t org

WHERE a.fin_coa_cd = account.fin_coa_cd
AND   a.account_nbr = account.account_nbr
AND   account.fin_coa_cd = org.fin_coa_cd
AND   account.org_cd = org.org_cd
AND   [$where_fin_coa_cd]
AND   [$where_rc_cd]
END-SELECT

print ' ' (+1,2)

end-procedure


begin-procedure PrintCampusInfo

   print 'CAMPUS TOTALS' (+3,2)
   print ' ' (+2,2)

end-procedure



begin-procedure PrintCampusTotals

   let $where_fin_coa_cd = 'a.fin_coa_cd = ''' || $old_fin_coa_cd || ''''

BEGIN-SELECT
'CMPS TOTAL'                                               (+2,15)
SUM(a.org_tot_bdgt_amt)  as c_total1                       (,27)   edit 9999,999,999,999.99
SUM(a.org_tot_actl_amt)  as c_total2                       (,47)   edit 9999,999,999,999.99
SUM(a.org_tot_bdgt_amt - a.org_tot_actl_amt) as c_total3   (,67)   edit 9999,999,999,999.99
SUM(a.org_tot_avail_amt) as c_total4                       (,87)   edit 9999,999,999,999.99
SUM(a.org_tot_encum_amt) as c_total5                       (,107)  edit 9999,999,999,999.99

FROM  gl_org_rvrsn_ctgry_amt_t a

WHERE (a.org_tot_bdgt_amt <> 0 OR a.org_tot_actl_amt <> 0 OR a.org_tot_avail_amt <> 0)
AND   [$where_fin_coa_cd]
END-SELECT


BEGIN-SELECT
'CMPS BUDGET BALANCE CARRIED FORWARD'                            (+2,38)
SUM(a.org_tot_cf_amt)  as rc_total6                              (,87)  edit 9999,999,999,999.99
'CMPS BUDGET BALANCE REVERTED'                                   (+1,45)
SUM(a.org_tot_rvrsn_amt) as rc_total7                            (,87)  edit 9999,999,999,999.99

FROM  gl_org_rvrsn_unit_wrk_t a

WHERE [$where_fin_coa_cd]
END-SELECT

print ' ' (+1,2)

end-procedure


begin-procedure MakeNewPageRenum

   new-page
   let #change_page_number = 1

end-procedure
