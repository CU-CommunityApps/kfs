!
! Copyright 2006 The Kuali Foundation.
! 
! Licensed under the Educational Community License, Version 1.0 (the "License");
! you may not use this file except in compliance with the License.
! You may obtain a copy of the License at
! 
! http://www.opensource.org/licenses/ecl1.php
! 
! Unless required by applicable law or agreed to in writing, software
! distributed under the License is distributed on an "AS IS" BASIS,
! WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
! See the License for the specific language governing permissions and
! limitations under the License.
! DO NOT add comments before the blank line below, or they will disappear.

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $Id: 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

#include "kul_error.h"
#include "kul_check.h"
#include "kul_common.h"
#include "kul_enter.h"


begin-setup
 !   page-size
    declare-layout default
    ! paper-size=(8.5,11)
      max-lines=62
      max-columns=150
      left-margin=0
      top-margin=0
    end-declare
end-setup


begin-heading 14

   #debug display 'entering HEADING'

   if #change_page_number = 1
      #debug display '- change_page_number is set'
      let #page_number = 1
      let #change_page_number = 0
   else
      let #page_number = #page_number + 1
   end-if

   #debug display 'current page number is ' NOLINE
   #debug display #page_number

   if #print_lf = 1
      #debug display '- printing linefeed'
      let $lb = chr(12) 
      print $lb (1,1)
      print ' .' (,2) 
   end-if

   ! add a couple spaces to the first line on each page.
   print '  ' (,1)
   print $report_date (+2,2)
   print 'PAGE' (,131)
   print #page_number (,135,8) edit 99999999


   print 'FOR THE MONTH ENDING ' (+2,2)
   print $display_for_month (,23)
   print 'ACCOUNT NUMBER:' (,93)
   print $old_account_nbr (,115,7)
   print $old_sub_acct_nbr (,124,5)
   print $old_fin_coa_cd (,131,2)

   print 'PERIOD:' (+1,2)
   print $display_univ_fscpd_cd (,10) edit 99
   print 'EFFECTIVE DATE:' (,43)
   print $old_acct_effect_dt (,60)
   print 'FISCAL OFFICER:' (,93)
   print $old_mgr_person_nm (,115,27)

   print 'ACCOUNT TRANSACTIONS' (+1,2)
   print 'EXPIRATION DATE:' (,43)
   print $old_acct_expiration_dt (,60)
   print 'ACCOUNT TITLE:' (,93)
   print $old_account_nm (,115,27)

   print 'REPORT ID:  ACCT_TRANS' (+1,2)
   print 'SUB FUND GROUP:' (,43)
   print $old_sub_fund_grp_desc (,59)
   print 'SUB-ACCOUNT TITLE:' (,93)
   print $old_sub_acct_nm (,115,27)

   print 'ORGANIZATION TITLE:' (+1,93)
   print $old_org_nm (,115, 27)

   print 'RC TITLE:' (+1,93)
   print $old_rc_shrt_nm (,115,27)


   print 'REFERENCE' (+2,79)
   print 'ORG' (,97)
   print 'ORG' (,137)

   print 'OBJECT' (+1,6)
   print 'DOC' (,15)
   print 'DOCUMENT' (,23)
   print 'DOCUMENT' (,80)
   print 'DOCUMENT' (,94)
   print 'PROJECT' (,107)
   print 'REFERENCE' (,134)

   print 'CODE' (+1,7)
   print 'TYPE' (,15)
   print 'NUMBER' (,24)
   print 'DESCRIPTION' (,51)
   print 'NUMBER' (,81)
   print 'NUMBER' (,95)
   print 'CODE' (,108)
   print 'AMOUNT' (,122)
   print 'ID' (,137)


   print '--------' (+1,5)
   print '----' (,15)
   print '--------------' (,20)
   print '----------------------------------------' (,36)
   print '--------------' (,77)
   print '----------' (,93)
   print '----------' (,105)
   print '---------------' (,117)
   print '--------' (,134)

   let #line_count = 0
   let #print_lf = 0

end-heading




begin-footing 9

   if #print_footer = 1 
 
      print 'CASH BALANCE' (,2) 

      print 'BEGINNING OF MONTH:' (+1,2)
      print $old_begin_cash (,22) edit 9999,999,999,999.99pf
      print 'CHANGE IN CASH BALANCE:' (,44)
      print $old_mocash (,68) edit 9999,999,999,999.99pf
      print 'CURRENT CASH BALANCE:' (,90)
      print $old_cash_balance (,112) edit 9999,999,999,999.99pf

      print 'INFORMATION ABOUT THIS REPORT' (+1,2)
      print 'THE ACCOUNT TRANSACTION REPORT PROVIDES A LISTING OF ACTUAL AND BUDGET TRANSACTIONS' (+1,2)
      print 'AND A LISTING OF OUTSTANDING ENCUMBRANCES.  THE ENCUMBRANCES INCLUDE INTERNAL, EXTERNAL, AND PRE-ENCUMBRANCES.' (+1,2)
      print 'TRANSACTIONS WITH DESCRIPTIONS BEGINNING WITH ''AUTO FR CHXXXXXXX'' ARE CONTINUATION ACCOUNT TRANSACTIONS.  THE CHART WHERE' (+1,2)
      print 'THE TRANSACTION ORIGINATED WILL REPLACE THE CHARACTERS CH, AND THE ACCOUNT NUMBER WILL REPLACE THE XXXXXXX.' (+1,2)

      let #print_footer = 0

   end-if

end-footing




begin-program

   Do GetParms
   Do Main

end-program


begin-procedure GetParms

   Do SetConstantGlobal

   input $fiscal_year 'Enter Fiscal Year'
   input $fiscal_period 'Enter Fiscal Period' 

   let $display_univ_fscpd_cd = $fiscal_period

   let #calendar_yr = to_number( $fiscal_year )
   let #fiscal_period = to_number( $fiscal_period )
   if ( #fiscal_period <= 6 )
      let #calendar_yr = #calendar_yr - 1
   end-if
   let $calendar_yr = to_char( #calendar_yr )

   ! determine the number of days in February for the current year
   Do SetFebruaryEnd( #calendar_yr, $feb_day_last )

   evaluate $display_univ_fscpd_cd
      when = '01'
         let $display_for_month = 'JULY 31, ' || $calendar_yr
         break
      when = '02'
         let $display_for_month = 'AUG. 31, ' || $calendar_yr
         break
      when = '03'
         let $display_for_month = 'SEP. 30, ' || $calendar_yr
         break
      when = '04'
         let $display_for_month = 'OCT. 31, ' || $calendar_yr
         break
      when = '05'
         let $display_for_month = 'NOV. 30, ' || $calendar_yr
         break
      when = '06'
         let $display_for_month = 'DEC. 31, ' || $calendar_yr
         break
      when = '07'
         let $display_for_month = 'JAN. 31, ' || $calendar_yr
         break
      when = '08'
         let $display_for_month = 'FEB. ' || $feb_day_last || ', ' || $calendar_yr
         break
      when = '09'
         let $display_for_month = 'MAR. 31, ' || $calendar_yr
         break
      when = '10'
         let $display_for_month = 'APR. 30, ' || $calendar_yr
         break
      when = '11'
         let $display_for_month = 'MAY 31, ' || $calendar_yr
         break
      when = '12'
         let $display_for_month = 'JUNE 30, ' || $calendar_yr
         break
      when = '13'
         let $display_for_month = 'JUNE 30, ' || $calendar_yr
         break
      when-other
         let $display_for_month = ' '
   end-evaluate

end-procedure




begin-procedure Main

   ! Set any variables 
   let #page_number = 0
   let #total_object = 0
   let #total_object_cd = 0
   let #just_changed_account = 1

BEGIN-SELECT
TO_CHAR(SYSDATE, 'MM/DD/YY')    &report_date
account_nbr                     &account_nbr        ()        ON-BREAK LEVEL=1 PRINT=NEVER PROCEDURE=MakeNewPageRenum
account_nbr as a2                                   ()        ON-BREAK LEVEL=1 PRINT=NEVER AFTER=PrintFooter
sub_acct_nbr                    &sub_acct_nbr       ()        ON-BREAK LEVEL=2 PRINT=NEVER PROCEDURE=MakeNewPage
fin_coa_cd                      &fin_coa_cd
TO_CHAR(incptn_dt,'MM/DD/YYYY') &acct_effect_dt
person_nm                       &mgr_person_nm
TO_CHAR(exprtn_dt,'MM/DD/YYYY') &acct_expiration_dt
account_nm                      &account_nm
fund_grp_cd                     &fund_grp_cd
sub_fund_grp_cd                 &sub_fund_grp_cd
sub_fund_grp_desc               &sub_fund_grp_desc
sub_acct_nm                     &sub_acct_nm
org_nm                          &org_nm
rc_shrt_nm                      &rc_shrt_nm
balance_name                    &balance_name
object_name                     &object_name
object_type                     &object_type        
object_type || balance_name as o                    ()        ON-BREAK LEVEL=3 PRINT=NEVER BEFORE=PrintObjectInfo
object_type || balance_name as o2                   ()        ON-BREAK LEVEL=3 PRINT=NEVER AFTER=PrintObjectSubtotal
object_code as oc2              &object_cd          ()        ON-BREAK LEVEL=4 PRINT=NEVER AFTER=PrintObjectCdTotal
object_code                                         (+1,5,8)
fdoc_typ_cd                                         (,15,4)
fdoc_nbr                                            (,20,14)
trn_ldgr_entr_desc                                  (,36,40)
fdoc_ref_nbr                                        (,77,14)
org_doc_nbr                                         (,93,10)
project_cd                                          (,105,10)
amount                          &amount             (,117,15) edit 99,999,999.99pf 
org_reference_id                                    (,134,8)
begin_cash                      &begin_cash
mocash                          &mocash
cash_balance                    &cash_balance
fin_obj_cd_shrt_nm              &fin_obj_cd_shrt_nm


   let $report_date = &report_date
   let $old_account_nbr = &account_nbr
   let $old_sub_acct_nbr = &sub_acct_nbr
   let $old_fin_coa_cd = &fin_coa_cd
   let $old_acct_effect_dt = &acct_effect_dt
   let $old_mgr_person_nm = &mgr_person_nm
   let $old_acct_expiration_dt = &acct_expiration_dt
   let $old_account_nm = &account_nm
   let $old_sub_fund_grp_cd = &sub_fund_grp_cd
   let $old_sub_fund_grp_desc = &sub_fund_grp_desc
   let $old_sub_acct_nm = &sub_acct_nm
   let $old_org_nm = &org_nm
   let $old_rc_shrt_nm = &rc_shrt_nm
   let $old_balance_name = &balance_name
   let $old_fund_grp_cd = &fund_grp_cd
   let $old_object_name = &object_name
   let #amount = &amount
   let $old_begin_cash = &begin_cash
   let $old_mocash = &mocash
   let $old_cash_balance = &cash_balance
   let $old_fin_obj_cd_shrt_nm = &fin_obj_cd_shrt_nm
   let $old_object_cd = &object_cd

   let #total_object = #total_object + #amount 
   let #total_object_cd = #total_object_cd + #amount

   let #just_changed_account = 0


FROM     kul_acct_trans_v

ORDER BY fin_coa_cd,
         account_nbr,
         sub_acct_nbr,
         object_type,
         balance_name,
         object_code,
         fdoc_typ_cd,
         fdoc_nbr

END-SELECT

end-procedure



begin-procedure MakeNewPageRenum

   new-page
   let #change_page_number = 1
   let #just_changed_account = 1

end-procedure


begin-procedure PrintFooter

   if $old_sub_fund_grp_cd <> 'GENFND'
      let #print_footer = 1 
   end-if

end-procedure


begin-procedure MakeNewPage

   new-page
   let #just_changed_account = 1

end-procedure


begin-procedure PrintObjectInfo

   let $object_name = &object_name
   let $balance_name = &balance_name

   if #just_changed_account = 0 
      print '*********************************************************************************************************************' (+1,2)
      print ' ' (+1,2)
   end-if

   if $balance_name = 'OPEN ENCUMBRANCES'
      print 'OPEN ENCUMBRANCES' (+1,2)
      print ' ' (+2,2)
   else
      print $object_name (+1,2)
      print $balance_name (+1,2)
      print ' ' (+1,2)
   end-if

end-procedure


begin-procedure PrintObjectSubtotal

   if $old_balance_name = 'ACTUAL' or $old_balance_name = 'ACTUAL (ACC VCHR)'
      let $total_object = #total_object
      let $line = 'TOTAL FOR ' || $old_balance_name || ' ' || $old_object_name

      print '---------------' (+1,117)
      print $line (+1,2)
      print $total_object (,117,15) edit 99,999,999.99pf
      print ' ' (+3,2)
   end-if

   let #total_object = 0

end-procedure

begin-procedure PrintObjectCdTotal

   if $old_fund_grp_cd = 'AE'
       
      let $total_object_cd = #total_object_cd
      let $line = '**TOTAL OBJECT CODE ' || $old_object_cd || ' ' || $old_fin_obj_cd_shrt_nm

      print '---------------' (+2,117)
      print $line (+1,10)
      print $total_object_cd (,117,15) edit 99,999,999.99pf
      print ' ' (+3,10)

   else
      print ' ' (+1,2)
   end-if

   let #total_object_cd = 0

end-procedure

